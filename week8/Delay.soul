// Stereo delay modified from SOUL example 
graph DelayGraph
{
    input  stream float<2> audioIn;
    output stream float<2> audioOut;

    input 
    {
        mixer.mix delayWet;
        delay.delayLength;
        delay.delayFeedback;
    }
    
    let
    {
        mixer = Mixer;
        delay = Delay;
    }
    
    connection
    {
        audioIn -> mixer.in1;

        audioIn -> delay.audioIn; 
        delay.audioOut -> mixer.in2;

        mixer.out -> audioOut;
    }
}

/*
    == SOUL example code ==
    == Author: Jules ==
*/

/// Title: A very simple delay example
///
/// This example is a very simple implementation of a delay, where the length
/// and feedback level can be changed dynamically with event parameters.

processor Delay
{
    input  stream float<2> audioIn;
    output stream float<2> audioOut;

    input event
    {
        float delayLength    [[ min: 0,  max: 1000, init:  50,  name: "Length",   unit: "ms", step: 1 ]];
        float delayFeedback  [[ min: 0,  max: 100,  init:  25,  name: "Feedback", unit: "%",  step: 1 ]];
    }

    event delayLength (float delayMs)
    {
        let delaySamples = max (1, int (processor.frequency * (delayMs / 1000.0f)));

        readPos = wrap<maxDelayLength> (writePos - delaySamples);
    }

    event delayFeedback (float f)
    {
        feedback = f * 0.01f;
    }

    let maxDelayLength = 100000;

    float feedback;
    float<2>[maxDelayLength] buffer;
    wrap<maxDelayLength> readPos, writePos;

    void run()
    {
        loop
        {
            buffer[writePos] = audioIn + feedback * buffer[readPos];
            audioOut << buffer[readPos];

            ++readPos;
            ++writePos;

            advance();
        }
    }
}
